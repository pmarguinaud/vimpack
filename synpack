#!/usr/bin/perl -w

=head1 NAME

synpack

=head1 SYNOPSIS

  # Modify the code
  [ECA 49t1_test1.03.IMPIIFC2018.x]$ vimpack stepo.F90                                
  # Merge modifications in another pack
  [ECA 49t1_test1.03.IMPIIFC2018.x]$ synpack --pack1 ../49t1_test2.03.IMPIIFC2018.x     

=head1 DESCRIPTION

Report modifications from one pack into another, using C<gitpack>.

=head1 PRINCIPLES

When invoked, C<synpack> will do the following:

=over 4

=item 1

Attempt to compile the code in the current pack, if C<MASTERODB> is not up-to-date.

=item 2

Commit the last changes with the message passed with the C<--message> option.

=item 3

Try to merge the current branch into the pack passed with C<--pack1> option.

=item 4

Try to compile C<MASTERODB> in the passed with C<--pack1> option.

=back

If step 3 or 4 fails, an interactive shell will be created and the user will be expected to :

=over 4

=item

If step 3 has failed, finish the merge by hand and commit the last modifications in pack1.

=item

If step 4 has failed, fix the code and commit the last modifications in pack1.

=back

=head1 SEE ALSO

C<gmkpack>, C<gitpack>, C<vimpack>

=head1 AUTHOR

pmarguinaud@hotmail.com

=cut

use strict;
use FileHandle;
use Cwd;
use File::stat;
use File::Find;
use File::Spec;
use File::Copy;
use File::Path;
use File::Basename;
use Getopt::Long;
use Data::Dumper;
use Term::ANSIColor;
use FindBin qw ($Bin);

use lib "$Bin/lib";

use bt;


sub runCommand
{
  my %args = @_; 
  my @cmd = @{ ${args}{command} };

  my $cwd = &cwd ();

  if ($args{chdir})
    {
      chdir ($args{chdir}) or die ("Cannot chdir $args{chdir}\n");
    }

  print "@cmd\n" if ($args{verbose});

  my $c = system (@cmd);

  if ($c && (my $eh = $args{errorHandler}))
    {
      $c = $eh->(@cmd);
    }

  chdir ($cwd);

  $c and die ("Command `@cmd' failed\n");
}

sub gitpack
{
  my %args = @_; 

  my ($pack, $cwd) = ($args{pack}, &cwd ());

  $pack && chdir ($pack);

  my @cmd = ('gitpack', @{ ${args}{command} });

  print "cwd=" . &cwd () . "\n@cmd\n" if ($args{verbose});

  my $out = `@cmd`;
  my $c = $?;

  if ($c && (my $eh = $args{errorHandler}))
    {
      $c = $eh->(@cmd);
    }

  chdir ($cwd);

  die ("Gitpack command `@cmd' failed\n")
    if ($c);

  return $out;
}

sub needReBuild
{
  # Returns true if MASTERODB is not up-to-date

  my %args = @_;

  my $pack = $args{pack} || &cwd ();

  my $MASTERODB = "$pack/bin/MASTERODB";

  return 1 unless (-f $MASTERODB);

  my $t0 = stat ("$pack/bin/MASTERODB")->mtime ();

  my $rebuild = 0;

  &find ({wanted => sub 
  {  
    my $f = $File::Find::name;
    return unless ((-f $f) && ($f =~ m/\.(?:F|F90|fypp|hypp|c|h|cpp|cc)$/io));
    my $st = stat ($f);
    $rebuild ||= $st->mtime () > $t0;
  }, no_chdir => 1}, "$pack/src/local/");

  return $rebuild;
}

sub slurp
{
  my $f = shift;
  (my $fh = 'FileHandle'->new ("<$f")) or die ("Cannot open `$f'\n");
  local $/ = undef;
  my $data = <$fh>;
  return $data;
}

sub filesEqual
{
  my ($f0, $f1) = @_;
  return &slurp ($f0) eq &slurp ($f1);
}


sub getBranch
{
  chomp (my $branch = &gitpack (@_, command => [qw (rev-parse --abbrev-ref HEAD)]));
  return $branch;
}

sub getStatus
{
  my $status = &gitpack (@_, command => [qw (status --porcelain)]);
  return $status;
}

sub getCommit
{
  chomp (my $commit = &gitpack (@_, command => [qw (rev-parse HEAD)]));
  return $commit;
}

sub rebuildIfNeeded
{
  # Build MASTERODB if not up-to-date

  my %args = @_;
  my $pack = $args{pack} || &cwd ();

  &runCommand (%args, command => ['./ics_masterodb'], chdir => $pack)
    if (&needReBuild (pack => $pack));
}

sub getView
{
  # Return view : local => path-to-local pack, inter.1 => path-to-inter1 pack, etc...
  
  my %args = @_;

  $args{local} = 1 unless (exists $args{local});

  my $pack = $args{pack};

  my @view = do { my $fh = 'FileHandle'->new ("<$pack/.gmkview"); <$fh> };
  chomp for (@view);

  my @pack = (shift (@view) => $pack);

  unless ($args{local})
    {
      @pack = ();
    }

  for my $view (@view)
    {
      (my $link = readlink ("src/$view")) or die;
      push @pack, $view, &dirname (&dirname ($link));
    }

  return @pack;
}

my %opts = (qw (verbose 0 message Commit method merge), pack0 => &cwd ());

my @opts_f = qw (push up verbose help debug);
my @opts_s = qw (message pack0 pack1 method);

sub help
{
  print "Usage: " . &basename ($0) . "\n" 
      . " Options:\n" . join ('', map { "   --$_\n" } @opts_f, @opts_s);
}

if (-f '.synpack.conf')
  {
    unshift (@ARGV, @{ do ("./.synpack.conf") || [] });
  }

&GetOptions
(
  (map { ($_, \$opts{$_}) } @opts_f),
  (map { ("$_=s", \$opts{$_}) } @opts_s),
);

if ($opts{method} !~ m/^(?:merge|cherry-pick)$/o)
  {
    die (&basename ($0) . " --method merge | cherry-pick");
  }

if ($opts{help})
  {
    print "Usage: " . &basename ($0) . "\n" 
        . " Options:\n" . join ('', map { "   --$_\n" } @opts_f, @opts_s);
    exit (0);
  }

my $pack0 = 'File::Spec'->rel2abs ($opts{pack0});

my @view = &getView (pack => $pack0, local => 0);

if ($opts{'up'})
  {
    $opts{pack1} = scalar (@view) ? $view[1] : undef;
  }

my $pack1 = 'File::Spec'->rel2abs ($opts{pack1});

# Detect the case of merging into parent pack

$opts{'up'} = scalar (@view) > 0 && ($view[1] eq $pack1) ? $view[0] : undef;

# Change pack0 & pack1 are packs

for my $opt (qw (pack0 pack1))
  {
    die unless ($opts{$opt});
    die unless (-f "$opts{$opt}/.genesis");
  }

my $branch0 = &getBranch (pack => $pack0);
my $branch1 = &getBranch (pack => $pack1);

sub PP
{
  if ($opts{debug})
    {
      print "\n" x 4;
      print "@_\n" x 4;
      print "\n" x 4;
    }
}

&PP ("REBUILD $pack0");


#================================== PACK0 =====================================

# Build, commit & push pack0

&rebuildIfNeeded (%opts, pack => $pack0);

if (&getStatus (%opts, pack => $pack0))
  {
    &PP ("COMMIT IN $pack0");
    &gitpack (%opts, pack => $pack0, command => [qw (commit -a), -m => $opts{message}]);
  }

if ($opts{push})
  {
    &gitpack 
      (%opts, pack => $pack0, command => [qw (push origin), $branch0]);
  }

my $BASHRC = << 'EOF';

if [ -f ~/.bashrc ]
then
  . ~/.bashrc
fi

if [ -f ~/.bash_profile ]
then
  . ~/.bash_profile
fi

pwd

EOF

#================================== PACK1 =====================================

&PP ("MERGE IN $pack1");

#                               *** MERGE ***

my ($remote) = &gitpack (%opts, pack => $pack1, command => ['remote']);
my @remote = split (m/\n/o, $remote);

&gitpack (%opts, pack => $pack1, command => [qw (remote remove pack0)]) if (grep { $_ eq 'pack0' } @remote);
&gitpack (%opts, pack => $pack1, command => [qw (remote add pack0), "$pack0/.gitpack/git"]);
&gitpack (%opts, pack => $pack1, command => [qw (fetch -a pack0)]);

&gitpack 
  (%opts, pack => $pack1, command => [$opts{method}, "pack0/$branch0"], errorHandler => sub
{
  my @cmd = @_;

  my $MERGE = &colored ('[MERGE]', 'black on_cyan');

  'FileHandle'->new ('>.synpack.sh')->print (<< "EOF");

$BASHRC

export PS1="$MERGE-$ENV{PS1}"

gitpack status

echo "PLEASE MERGE MANUALLY; DO NOT FORGET TO COMMIT YOUR CHANGES"

EOF

  chmod (0755, 'cmd.sh');

  while (&getStatus (%opts, pack => $pack1))
    {
      if (system (qw (bash --init-file ./.synpack.sh -i)))
        {
          die;
        }
    }

  return 0;
});

&gitpack (%opts, pack => $pack1, command => [qw (remote remove pack0)]);

&PP ("REBUILD $pack1");

#                               *** BUILD ***

&rebuildIfNeeded (%opts, pack => $pack1, errorHandler => sub
{
  my @cmd = @_;

  my $BUILD = &colored ('[BUILD]', 'black on_cyan');

  'FileHandle'->new ('>.synpack.sh')->print (<< "EOF");

$BASHRC

export PS1="$BUILD-$ENV{PS1}"

echo "PLEASE COMPILE MANUALLY; DO NOT FORGET TO COMMIT YOUR CHANGES"

EOF

  chmod (0755, 'cmd.sh');

  while ((! -f "$pack1/bin/MASTERODB") or &getStatus (%opts, pack => $pack1))
    {
      if (system (qw (bash --init-file ./.synpack.sh -i)))
        {
          die;
        }
    }
  
  
  return 0;
});

#                               *** PUSH ***

if ($opts{push})
  {
    &gitpack 
      (%opts, pack => $pack1, command => [qw (push origin), $branch1]);
  }

&PP ("UP");


#================================== PACK0 =====================================

if (my $view1 = $opts{up})
  {
    # pack1 is the parent of pack0

    # Update dependency database

    my $text = &slurp ("$pack1/src/.gmak/local.sds");
    $text =~ s/'local'/'$view1'/gms;
    'FileHandle'->new (">$pack0/src/.gmak/$view1.sds")->print ($text);

    # The following command will remove files from pack0 which are identical in pack1

    &gitpack (%opts, pack => $pack0, command => [qw (checkout -f -q), $branch0]);

    # Rebuild pack0

    &runCommand (command => ['cleanpack', '-f'], chdir => $pack0);
    &runCommand (command => ['resetpack', '-f'], chdir => $pack0);
    &runCommand (command => ['./ics_masterodb'], chdir => $pack0);

    # Rebuild vimpack index

    &rmtree ("$pack0/.vimpack");
    &runCommand (command => ['vimpack', '-i'], chdir => $pack0);

  }




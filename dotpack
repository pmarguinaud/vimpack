#!/usr/bin/perl -w

=head1 NAME

dotpack

=head1 SYNOPSIS

  [CNC pack]$ cd 48t3_cpg_drv+.01.PGI2211.cpu0
  [CNC 48t3_cpg_drv+.01.PGI2211.cpu0]$ dotpack CPG_GP_HYD 
  [CNC 48t3_cpg_drv+.01.PGI2211.cpu0]$ ls -l CPG_GP_HYD.svg 
  -rw-r--r-- 1 marguina mrpm 14975 Mar 30 16:17 CPG_GP_HYD.svg

=head1 DESCRIPTION

C<dotpack> relies on graphviz to create call graphs.

=head1 SEE ALSO

C<gmkpack>, graphviz

=head1 AUTHORS

pmarguinaud@hotmail.com

=cut

package scan;

use strict;
use Data::Dumper;
use FileHandle;
use File::Basename;
use File::Find;

my %data;

sub slurp
{
  my ($file, $discard) = @_;

  unless (exists $data{$file})
    {
      my $fh = 'FileHandle'->new ("<$file");
      local $/ = undef;
      $data{$file} = <$fh>;
    }

  return delete ($data{$file}) if ($discard);

  return $data{$file};
}

sub scan
{
  shift;

  my %f2f;
  my %seen;
  
  my $wanted = sub
  {
    return unless (m/\.F90$/o);
    my $f = $File::Find::name;
    return if ($seen{&basename ($f)}++);
    my $code = &slurp ($f, 1);
    my ($s) = ($code =~ m/(?:MODULE|FUNCTION|PROGRAM|SUBROUTINE)[ ]+(\w+)/goms);
    return unless ($s);
    $s = uc ($s);
    $f2f{$s} = $f;
  };
  
  my @view = do { my $fh = 'FileHandle'->new ("<.gmkview"); my @v = <$fh>; chomp for (@v); @v };
  
  if (-f 'f2f.pl')
    {
      %f2f = %{ do ('./f2f.pl') };
      pop (@view) while (scalar (@view) > 1);
    }
  
  for my $view (@view)
    {
      &find ({wanted => $wanted, no_chdir => 1}, "src/$view/");
    }
  
  'FileHandle'->new (">f2f.pl")->print (&Dumper (\%f2f));

  return \%f2f;
}


my %call;

sub call
{
  shift;

  my $file = shift;

  unless (exists $call{$file})
    {
      my $code = uc (&slurp ($file));
      my @code = grep { !/^\s*!/o } split (m/\n/o, $code);
      $code = join (" ; ", @code);

      my @call = ($code =~ m/\bCALL\s+(\S+)/goms);

      for (@call)
        {
          s/\(.*//o;
        }


      $call{$file} = \@call;
    }

  return $call{$file};
}

my %size;

sub size
{
  shift;

  my $file = shift;
  
  die $file unless (-f $file);

  unless (exists $size{$file})
    {
      my @code = do { my $fh = 'FileHandle'->new ("<$file"); <$fh> };
      $size{$file} = scalar (@code);
    }

  return $size{$file};
}

package drhook;

use strict;

sub read
{
  shift;
  my ($f) = @_; 

  my @line = do { my $fh = 'FileHandle'->new ("<$f"); <$fh> };
  shift (@line) for (1 .. 13); 

  my %Name;

  for my $line (@line)
    {   
      chomp ($line);

      $line =~ s/^\s*(\S+)\s+(\S+)\s+(\S+)\s+(\S+)\s+(\S+)\s+(\S+)\s+(\S+)\s+(\S+)\s+//go;
      my ($Rank, $Time, $Cumul, $Self, $Total, $Calls, $SelfPerCall, $TotalPerCall) = ($1, $2, $3, $4, $5, $6, $7, $8);


      $line =~ s/^\*//o;
      my ($Name, $Thread);

      unless (($Name, $Thread) = ($line =~ m/^(.*)\@(\d+)$/o))
        {
          ($Name, $Thread) = ($line, 1); 
        }

      $Name{$Name} = 1;
    }   

  return \%Name;
}


package main;

use strict;
use Data::Dumper;
use FileHandle;
use File::Basename;
use FindBin qw ($Bin);
use Getopt::Long;

use lib "$Bin/perl5";

eval "use GraphViz2;";

if ($@)
  {
    die ("Please install the GraphViz2 module\n");
  }

my @PATH = split (m/:/o, $ENV{PATH});

for my $path (@PATH)
  {
    goto FOUND if (-f "$path/dot");
  }

die ("The `dot' executable (from graphviz) was not found in your PATH\n");

FOUND:

my @opts_s = qw (rankdir skip drhook drhook-color);
my @opts_f = qw (drhook-skip);
my %opts = (rankdir => 'LR', 'skip' => '', drhook => '', 'drhook-color' => 'red');
&GetOptions 
(
  map ({ ("$_=s", \$opts{$_}) } @opts_s),
  map ({ ("$_", \$opts{$_}) } @opts_f),
);

my $f2f = 'scan'->scan ();

my %drhook;

if ($opts{drhook})
  {
    %drhook = %{ 'drhook'->read ($opts{drhook}) };
  }

my %g; # Graph
my %L; # Size

my @q = @ARGV; # Routines to process

my %skip1 = map { ($_, 1) } qw (DR_HOOK ABOR1_SFX ABOR1 WRSCMR SC2PRG VERDISINT VEXP NEW_ADD_FIELD_3D ADD_FIELD_3D FMLOOK_LL
                                FMWRIT LES_MEAN_SUBGRID SECOND_MNH ABORT_SURF SURF_INQ SHIFT ABORT LFAECRR FLUSH LFAFER LFAECRI LFAOUV LFAECRC
                                LFAPRECR NEW_ADD_FIELD_2D ADD_FIELD_2D WRITEPROFILE WRITEMUSC WRITEPHYSIO CONVECT_SATMIXRATIO COMPUTE_FRAC_ICE
                                TRIDIA LFAPRECI PPP2DUST GET_LUOUT ALLOCATE DEALLOCATE PUT GET SAVE_INPUTS GET_FRAC_N DGEMM SGEMM),
                            split (m/,/o, $opts{skip});


sub add
{

# Add a routine to the graph

  my $name = shift;


  return if (exists $g{$name});
  $g{$name} ||= [];
  my $file = $f2f->{$name};

# Count line numbers

  unless ($file)
    {
      $L{$name} = '';
      return;
    }
  $L{$name} = 'scan'->size ($file);
}

for my $q (@q)
  {
    $drhook{$q} = 2 if ($drhook{$q});
    &add ($q);
  }

my %seen;

while (my $name = shift (@q))
  {
    my $file = $f2f->{$name};

    next unless ($file);

    my @call = @{ 'scan'->call ($file) };
    
    @call = 
       grep { ! m/^(?:GSTAT|MPL_|JFH_)/o } 
       grep { ! $skip1{$_} } @call;

    for my $call (@call)
      {
        &add ($call);
        push @{ $g{$name} }, $call;
        push @q, $call unless ($seen{$call}++);
        $drhook{$call} =2 if ($drhook{$call} && $drhook{$name});
      }
    
  }

# Single link, even for multiple calls

while (my ($k, $v) = each (%g))
  {
    my %seen;
    @$v = grep { ! ($seen{$_}++) } grep { $g{$_} } @$v;
  }

my @root = keys (%g); # Never called by any other routine belonging to the graph

while (my ($k, $v) = each (%g))
  {
    my %v = map { ($_, 1) } @$v;
    @root = grep { ! $v{$_} } @root;
  }

sub color
{
  my $name = shift;
  return unless ($drhook{$name} > 1);
  return (style => 'filled', fillcolor => $opts{'drhook-color'}, color => 'black');
}

my $root = join ('-', sort @root);

my $g = 'GraphViz2'->new (graph => {rankdir => $opts{rankdir}, ordering => 'out'}, global => {rank => 'source'});

while (my ($k, $v) = each (%g))
  {
    next if ($k =~ m/%/o); # Do not report method call

    my $skip = $opts{'drhook-skip'} && (($drhook{$k} || 0) < 2);
    next if ($skip);

    $g->add_node (name => $k, label => "$k\n$L{$k}", shape => 'box', &color ($k));
    for my $v (@$v)
      {   
        next if ($v =~ m/%/o); # Do not report method call

        my $skip = $opts{'drhook-skip'} && (($drhook{$v} || 0) < 2);
        next if ($skip);

        $g->add_edge (from => $k, to => $v);
      }   
  }

$g->run (format => 'svg', output_file => "$root.svg");


